buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url = "https://packages.confluent.io/maven"
        }
    }
    dependencies {
        classpath 'gradle.plugin.com.github.johnrengelman:shadow:7.1.2'
    }
}


plugins {
    id 'java' // so that we can use 'implementation', 'testImplementation' for dependencies
    id 'maven-publish'
    id 'signing'
}

apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'com.github.johnrengelman.shadow'

import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

repositories {
    jcenter()
    mavenCentral()
    maven {
        url = "https://packages.confluent.io/maven"
    }
    maven {
        url = "https://jitpack.io"
    }
}

group = 'io.github.ferozed.kafka.encryption'
version = '0.1-SNAPSHOT'

dependencies {

    shadow 'org.apache.avro:avro:1.10.1'
    // Kafka
    shadow "org.apache.kafka:kafka_2.12:2.2.0"
    implementation 'com.google.crypto.tink:tink:1.6.1'
    implementation 'com.google.crypto.tink:tink-awskms:1.6.1'
    shadow 'org.apache.commons:commons-lang3:3.2.1'
    shadow 'io.confluent:kafka-schema-registry-client:6.2.1'

    //test
    testImplementation(platform('org.junit:junit-bom:5.9.0'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation("org.mockito:mockito-bom:4.8.0")
    testImplementation("org.mockito:mockito-junit-jupiter:4.8.0")
    testImplementation('io.confluent:kafka-avro-serializer:6.1.2')
    testImplementation 'io.confluent:kafka-json-serializer:6.2.1'
    testImplementation 'io.confluent:kafka-json-schema-provider:6.2.1'
    testImplementation 'io.confluent:kafka-avro-serializer:6.2.1'
}

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java"]
        }

        resources {
            srcDirs = ["src/main/avro", "src/main/resources"]
        }
    }
    test {
        java {
            srcDirs = ["src/test/java"]
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

plugins.withId("com.github.johnrengelman.shadow"){

    //this block requires the java plugin to be applied first.
    plugins.withId("java"){

        shadowJar {
            //We are overriding the default jar to be the shadow jar
            classifier = "shadow"
            exclude 'META-INF'
            exclude 'META-INF/*.INF'
            exclude 'META-INF/license/*'
            //exclude 'org/apache/*'
            //exclude 'io/confluent/*'

            //relocate("com.google", "io.github.ferozed.com.google")
        }

        jar {
            manifest {
                attributes(
                        'Built-By'       : System.properties['user.name'],
                        'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                        'Created-By'     : "Gradle ${gradle.gradleVersion}",
                        'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                        'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
                )
            }
        }

        tasks.build.dependsOn tasks.shadowJar

        task relocateShadowJar(type: ConfigureShadowRelocation) {
            target = tasks.shadowJar
            prefix = "io.github.ferozed.shadow" // Default value is "shadow"

        }

        tasks.shadowJar.dependsOn tasks.relocateShadowJar

        tasks.shadowJar.mustRunAfter tasks.jar
        tasks.shadowJar.mustRunAfter tasks.javadocJar
        tasks.shadowJar.mustRunAfter tasks.sourcesJar

    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Mask Json Field Transform'
                description = 'A kafka connect transform to remove the value of a sensitive field in a json document.'
                url = 'https://github.com/ferozed/mask-json-field-transform'
                properties = [
                        myProp: "value",
                        "prop.with.dots": "anotherValue"
                ]
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'ferozed'
                        name = 'Feroze Daud'
                        email = 'ferozed.oss@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:ferozed/mask-json-field-transform.git'
                    developerConnection = 'scm:git:git@github.com:ferozed/mask-json-field-transform.git'
                    url = 'https://github.com/ferozed/mask-json-field-transform'
                }
            }
        }
    }


    repositories {
        maven {
            name = "OSSRH"
            def release_url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshot_url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshot_url : release_url
            credentials {
                username = project.ossrhUsername
                password = project.ossrhPassword
            }
        }
    }
}

signing {
    //sign configurations.archives
    sign publishing.publications.mavenJava
}

//tasks.signArchives.dependsOn tasks.shadowJar

artifacts {
    archives javadocJar, sourcesJar
}
